---
layout: post
title:  "Shader绘图"
image: ''
date:   2022-11-03 15:39:39
tags:
- Shader
description: ''
categories: 
- Shader
---
[在线shader编辑器](http://editor.thebookofshaders.com/)
```
step() 插值函数需要输入两个参数。  
第一个是极限或阈值，第二个是我们想要检测或通过的值。  
对任何小于阈值的值，返回 0.0，大于阈值，则返回 1.0。
```
```c
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform float u_time;

void main() {
    //将片段的坐标值映射到[0,1]的区间内 (归一化)
    vec2 st = gl_FragCoord.xy/u_resolution;

    // 如果片段的归一坐标大于0.5则返回1.0,否则返回0.0
    float y = step(0.5,st.x);
    //转变成颜色值,将会是图像左半边为(0,0,0,1) 右半边为(1,1,1,1)
    vec3 color = vec3(y);
    gl_FragColor = vec4(color,1.0);
}
```
![图片](..\assets\img\opengl\step.png)


```
smoothstep(float edge0,float edge1, float x)
如果变量x的值在如果在[edge0,edge1]之间,则返回插值后的值(范围[0,1]),
在范围之外的值,取边界值。

```
![图片](..\assets\img\opengl\smoothstep.png)  
```c
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform float u_time;

float plot(vec2 st, float pct){
  /*
    假如片段的X轴的值小于0.5,则pct等于0
    st代表片段的归一化坐标
    第一个smoothstep的几何意义是:
    如果片段Y轴坐标大于0则返回1
    第二个smoothstep的几何意思是:
    如果片段Y轴大于0.02则返回1
    两者相减,即:Y轴值在[0-0.02]之间为插值区域,其他地方为0

    如果pct等于1
    第一个smoothstep的几何意义是:
    如果片段Y轴坐标小于0.98则返回0
    第二个smoothstep的几何意思是:
    如果片段Y轴小于1 则返回0
    两者相减,即:Y轴值在[0.98-1]之间为插值区域,其他地方为0
  */
  

  return  smoothstep( pct-0.02, pct, st.y) -
          smoothstep( pct, pct+0.02, st.y);
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;
    float y = step(0.5,st.x);

    vec3 color = vec3(y);

    float pct = plot(st,y);
    //pct区间以外的地方用原有的颜色
    //pct指定的区间使用绿色和原有的颜色进行混合
    color = (1.0-pct)*color+pct*vec3(0.0,1.0,0.0);

    gl_FragColor = vec4(color,1.0);
}
```
![图片](..\assets\img\opengl\smoothstep2.png)  

```c
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform float u_time;

float plot(vec2 st, float pct){
  return  smoothstep( pct-0.02, pct, st.y) -
          smoothstep( pct, pct+0.02, st.y);
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;

    // 根据片段的归一化坐标X 返回[0.1-0.9]之间的插值
    // 每个X对应一个Y
    float y = smoothstep(0.1,0.9,st.x);
    //从黑色到白色的过渡
    vec3 color = vec3(y);

    //绘制绿色的线条,对绿色线条进行插值
    float pct = plot(st,y);
    color = (1.0-pct)*color+pct*vec3(0.0,1.0,0.0);

    gl_FragColor = vec4(color,1.0);
}

```
![图片](..\assets\img\opengl\smoothstep3.png)  



```c
mix(color1,color2,percent)
这个函数让你以百分比混合两个值

注意:这个percent取值为[0,1]之间,
同时这个值代表的是color2所占的比例
```

```c
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

vec3 colorA = vec3(0.149,0.141,0.912);
vec3 colorB = vec3(1.000,0.833,0.224);

void main() {
    vec3 color = vec3(0.0);

    float pct = abs(sin(u_time));

    // Mix uses pct (a value from 0-1) to
    // mix the two colors
    color = mix(colorA, colorB, pct);

    gl_FragColor = vec4(color,1.0);
}
```
[缓动函数效果网站](https://easings.net/zh-cn)