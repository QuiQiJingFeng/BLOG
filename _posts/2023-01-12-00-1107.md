---
layout: post
title:  "Shader 绘图"
image: ''
date:   2023-01-12 14:49:06
tags:
- Shader
description: ''
categories: 
- Shader
---
[参考文档](https://thebookofshaders.com/05/?lan=ch)  
[WebGL 中可视化 GLSL 函数](https://graphtoy.com/)
# 1、绘制一条求 X 的5次方的曲线
```c
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

float plot(vec2 st, float pct){
    // 第一个smoothstep [pct-0.02,pct]的区间  
    // 第一个smoothstep [pct,pct + 0.02]的区间
    // 因为smoothstep 区间外左边为0,右边为1,两个这样相减的话,就能保证两个区间组合外都为0
    // 相减的两个区间,必须保证第一个的有区间等于第二个的左区间=.+
    // 即[pct-0.02,pct+0.02]之外都为0,区间之内进行插值
  return  smoothstep( pct-0.02, pct, st.y) -
          smoothstep( pct, pct+0.02, st.y);
}

void main() {
    //将像素点坐标转换到[0,1]范围内
    vec2 st = gl_FragCoord.xy/u_resolution;
    //求出当前像素点的5次方的值y
    float y = pow(st.x,5.0);

    vec3 color = vec3(y);
    //取区间为[y-0.02,y+0.02]之间的值进行插值,区间外的值丢弃
    float pct = plot(st,y);
    //(1.0-pct)*color 对区间之外的值使用vec3(y)的颜色
    //pct*vec3(0.0,1.0,0.0) 对区间之内的值使用绿色
    color = (1.0-pct)*color+pct*vec3(0.0,1.0,0.0);

    gl_FragColor = vec4(color,1.0);
}

```
![图片](..\assets\img\opengl\x^5.png)

# 2、三角函数与圆的关系 
```lua
--根据给定的圆心和半径以及角度值返回一个圆上的点的坐标
local function getCirclePosition(center,radius,degree)
    --角度转弧度
    local radian = math.rad(degree)
    --当我们给出一个角度（这里采用弧度制），它就会返回半径为一的圆上一个点的 x 坐标（cos）和 y 坐标（sin）
    local x = center.x + math.cos(radian) * radius
    local y = center.y + math.sin(radian) * radius
    return cc.p(x,y)
end

--给定的圆心和两个轴的长度a、b,以及角度获取椭圆上的一个坐标
--通过对圆上的点X/Y轴进行不同程度的拉伸得到椭圆
local function getEllipsePosition(center,a,b,degree)
    --角度转弧度
    local radian = math.rad(degree)
    --当我们给出一个角度（这里采用弧度制），它就会返回半径为一的圆上一个点的 x 坐标（cos）和 y 坐标（sin）
    local x = center.x + math.cos(radian) * a
    local y = center.y + math.sin(radian) * b
    return cc.p(x,y)
end

--x/y的反正切函数 会返回一个介于-PI到PI的弧度值（-3.14 to 3.14）
--根据圆心坐标center以及圆的坐标点pos,获取pos所在点对应的角度
local function getCircleDegreeWithCenterAndPos(center,pos)
    local toCenter = cc.pSub(center,pos)
    local radian = math.atan2(toCenter.y,toCenter.x)
    radian = radian + math.pi
    return math.deg(radian)
end

--根据圆心坐标center以及圆的坐标点pos,获取pos所在点对应的角度
--注意:这里获取的是椭圆的角度,不是圆的角度
local function getEllipseDegreeWithCenterAndPos(center,pos)
    local toCenter = cc.pSub(center,pos)
    local radian = math.atan2(toCenter.y,toCenter.x)
    radian = radian + math.pi
    return math.deg(radian)
end

```

# 3、HSB 色相，饱和度和亮度
色相从0~1可以取到赤橙黄绿青蓝紫五个颜色
将x坐标（位置）映射到Hue值并将y坐标映射到明度，我们就得到了五彩的可见光光谱。这样的色彩空间分布实现起来非常方便，比起RGB，用HSB来拾取颜色更直观。  
```c
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

vec3 rgb2hsb( in vec3 c ){
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz),
                 vec4(c.gb, K.xy),
                 step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r),
                 vec4(c.r, p.yzx),
                 step(p.x, c.r));
    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)),
                d / (q.x + e),
                q.x);
}

//  Function from Iñigo Quiles
//  https://www.shadertoy.com/view/MsS3Wc
vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix(vec3(1.0), rgb, c.y);
}

void main(){
    //将像素坐标转换到[0,1]
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);

    // We map x (0.0 - 1.0) to the hue (0.0 - 1.0)
    // And the y (0.0 - 1.0) to the brightness
    //色相取像素的X坐标,亮度选择像素的Y坐标
    color = hsb2rgb(vec3(st.x,1.0,st.y));

    gl_FragColor = vec4(color,1.0);
}
```
![图片](..\assets\img\opengl\hsb.png)

### 极坐标下的HSB  

![图片](..\assets\img\opengl\hsb2.png)  
* 色调（H,hue）：在0~360°的标准色轮上，色相是按位置度量的。在通常的使用中，色相是由颜色名称标识的，比如红、绿或橙色。黑色和白色无色相。  
* 饱和度（S,saturation）：表示色彩的纯度，为0时为灰色。白、黑和其他灰色色彩都没有饱和度的。在最大饱和度时，每一色相具有最纯的色光。取值范围0～100%。  
* 亮度（B,brightness或V,value）：是色彩的明亮度。为0时即为黑色。最大亮度是色彩最鲜明的状态。取值范围0～100%。 


```HSB原本是在极坐标下产生的（以半径和角度定义）而并非在笛卡尔坐标系（基于xy定义）下。将HSB映射到极坐标我们需要取得角度和到像素屏中点的距离```

```
#ifdef GL_ES
precision mediump float;
#endif

#define TWO_PI 6.28318530718

uniform vec2 u_resolution;
uniform float u_time;

//  Function from Iñigo Quiles
//  https://www.shadertoy.com/view/MsS3Wc
vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix( vec3(1.0), rgb, c.y);
}

void main(){
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);

    // Use polar coordinates instead of cartesian
    vec2 toCenter = vec2(0.5)-st;
    float angle = atan(toCenter.y,toCenter.x);
    float radius = length(toCenter)*2.0;

    // Map the angle (-PI to PI) to the Hue (from 0 to 1)
    // and the Saturation to the radius
    color = hsb2rgb(vec3((angle/TWO_PI)+0.5,radius,1.0));

    gl_FragColor = vec4(color,1.0);
}
```


# 绘制带圆角的线条
```c
// License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
const vec4 hsv2rgb_K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
vec3 hsv2rgb(vec3 c) {
    vec3 p = abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www);
    return c.z * mix(hsv2rgb_K.xxx, clamp(p - hsv2rgb_K.xxx, 0.0, 1.0), c.y);
}

    

float random(float x){
    return fract(sin(x)*12561.1516);
}
float noise(float x){
    float i = floor(x);
    float f  = fract(x);
    float u = f*f*(3.0-2.0*f);
    return mix(random(i),random(i+1.0),u);
}

void main()
{
    vec2 uv = -1. + 2. * v_texCoord;  //[-1,1]
    uv.x *= resolution.x/resolution.y;  

    vec4 color = vec4(0.0,0.0,0.0,0.0);
    float len = 0.3;  //线条的长度
    uv = abs(uv);  //   左右上下都是正刻度
    uv.x -= len*0.5;  //即:线段内的x应该是小于等于0的,线段外的是大于0的
    float d0 = length(uv);  // 此时uv的坐标分别以两个端点为原点的坐标,所以距离就是距离端点的距离
    float d = uv.x > 0.0 ? d0 : uv.y;


    float g0 = 0.05/d;
    color.rgb += hsv2rgb(vec3(1.0, 0.8, g0));


    gl_FragColor = color;
}
```