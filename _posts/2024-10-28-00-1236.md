---
layout: post
title:  "cmake 学习一"
image: ''
date:   2024-10-28 18:05:06
tags:
- cmake
description: ''
categories: 
- cmake
---

```cmake
cmake_minimum_required(VERSION 3.5)
project(game)
#add_executable()命令告诉 CMake 使用指定的源代码文件创建一个可执行文件
add_executable(game test.cpp)
```
```c
//windows平台下生成makefile
mkdir build
cd build
cmake -G "Unix Makefiles" ..

//windows平台下生成.sh工程文件
mkdir build
cd build
cmake -G "Visual Studio 16 2019" ..
//或者cmake .. //不指定的话会自动查找Visual Studio版本使用
```

====================================================================
```cpp
//test.cpp
#include <stdio.h>
#include <string>
int main() {
    double num = std::stod("123.567");
    printf("FYD-----%f\n",num);
    return 0;
}

```
```cmake
cmake_minimum_required(VERSION 3.5)
project(game)

# 这行代码告诉 CMake 使用 C++11 标准进行编译。
# CMAKE_CXX_STANDARD 是一个变量，CMake 将根据该变量设置编译器的选项，以确保所使用的 C++ 标准。
set(CMAKE_CXX_STANDARD 11)

# 这行代码表示 CMake 必须使用指定的 C++ 标准（在本例中为 C++11）。
# 如果将其设置为 True，CMake 会确保编译器支持 C++11。如果编译器不支持该标准，则会报错。
set(CMAKE_CXX_STANDARD_REQUIRED True)

add_executable(game ../test.cpp)

```

================================================================
### 三方库处理
```cmake
cmake_minimum_required(VERSION 3.5)
project(game)

# 这行代码告诉 CMake 使用 C++11 标准进行编译。
# CMAKE_CXX_STANDARD 是一个变量，CMake 将根据该变量设置编译器的选项，以确保所使用的 C++ 标准。
set(CMAKE_CXX_STANDARD 11)

# 这行代码表示 CMake 必须使用指定的 C++ 标准（在本例中为 C++11）。
# 如果将其设置为 True，CMake 会确保编译器支持 C++11。如果编译器不支持该标准，则会报错。
set(CMAKE_CXX_STANDARD_REQUIRED True)

# 加载当前目录下的3rd目录的CMakeLists.txt,这个执行完后ext_util就生成了
add_subdirectory(3rd)

# 创建可执行目标game
add_executable(game ../test.cpp)

# 链接到可执行目标,注意顺序,先有目标才能链接
target_link_libraries(game PUBLIC ext_util)
```
```cmake
add_library(ext_util Util.cpp)

# PUBLIC：包含路径不仅对当前库可见，对使用该库的其他目标也可见
# 意味着链接到这个库之后不用加搜索目录了,因为已经加过了
target_include_directories(ext_util PUBLIC .)
```
```cpp
//Util.h
#ifndef __UTIL__H__
#define __UTIL__H__
#include <stdio.h>
#include <string>
class Util
{
    public:
        static std::string getVersioin();
};  //注意这里的分号是必须要加的
#endif
//Util.cpp
#include "Util.h"

std::string Util::getVersioin()
{
    return "123.456";
}
//test.cpp
#include <stdio.h>
#include <string>
#include "Util.h"
int main() {
    std::string version = Util::getVersioin();
    double num = std::stod(version);
    printf("FYD-----%f\n",num);
    return 0;
}

```

================================================================
## CMake 宏定义
target_compile_definitions 和 add_definitions 是 CMake 中用于定义预处理宏的命令，但它们的作用范围和使用方式有所不同。  
## 作用范围  
### target_compile_definitions： 

这是一个更现代和推荐的方式，用于为特定目标（库或可执行文件）添加编译时宏定义。  
宏定义的可见性可以通过 PRIVATE、PUBLIC 和 INTERFACE 关键字控制。  
**PRIVATE：宏仅在目标内部可见。**  
**PUBLIC：宏在目标内部和链接到该目标的其他目标中可见。**  
**INTERFACE：宏不在目标内部可见，但在链接到该目标的其他目标中可见。**    
```cmake
# example
add_library(MyLibrary src/MyLibrary.cpp)
target_compile_definitions(MyLibrary PRIVATE "USE_FEATURE_A")
```

### add_definitions：

用于为整个项目或所有后续目标添加编译时宏定义。  
不支持作用域控制，所有后续的目标都会受到影响。  
```cmake
#example
add_definitions(-DUSE_FEATURE_A)
```  

## 使用方式
target_compile_definitions 提供了更细粒度的控制，允许针对不同目标设置不同的宏定义。
add_definitions 更简单，但可能导致不必要的宏定义在不需要的目标中被使用。
## 推荐用法
CMake 官方文档建议使用 target_compile_definitions，因为它提供了更好的灵活性和控制，特别是在处理大型项目时。
```cmake
cmake_minimum_required(VERSION 3.5)
project(game)

# 这行代码告诉 CMake 使用 C++11 标准进行编译。
# CMAKE_CXX_STANDARD 是一个变量，CMake 将根据该变量设置编译器的选项，以确保所使用的 C++ 标准。
set(CMAKE_CXX_STANDARD 11)

# 这行代码表示 CMake 必须使用指定的 C++ 标准（在本例中为 C++11）。
# 如果将其设置为 True，CMake 会确保编译器支持 C++11。如果编译器不支持该标准，则会报错。
set(CMAKE_CXX_STANDARD_REQUIRED True)

# BOOL 类型功能开始或关闭 ON/OFF
option(PHYSICS_FEATURE "physics word" ON)


# 加载当前目录下的3rd目录的CMakeLists.txt文件
add_subdirectory(3rd)

# 创建可执行目标game
add_executable(game ../test.cpp)

target_compile_definitions(game PUBLIC "CC_PLATFORM_WIN32=1")
target_compile_definitions(game PUBLIC "CC_TARGET_PLATFORM=1")
if (PHYSICS_FEATURE)
    target_compile_definitions(game PUBLIC "PHYSICS_FEATURE")
endif()


# 链接到可执行目标,注意顺序,先有目标才能链接
target_link_libraries(game PUBLIC ext_util)
```
```c++
#include <stdio.h>
#include <string>
#include "Util.h"


int main() {
    std::string version = Util::getVersioin();
    double num = std::stod(version);
    printf("FYD-----%f\n",num);
#if CC_TARGET_PLATFORM == CC_PLATFORM_WIN32
    printf("FYD--CC_TARGET_PLATFORM---%d\n",CC_TARGET_PLATFORM);
#endif

#if PHYSICS_FEATURE
    printf("FYD--PHYSICS_FEATURE---\n");
#endif
    return 0;
}
```

================================================================
## 静态库和共享库
```cmake
# 创建静态库,会生成一个libext_util.a的静态库
add_library(ext_util STATIC Util.cpp)

# PUBLIC：包含路径不仅对当前库可见，对使用该库的其他目标也可见
# 意味着链接到这个库之后不用加搜索目录了,因为已经加过了
target_include_directories(ext_util PUBLIC .)
```
### 主CMakeList 共享库需要跟exe在同一个目录
```cmake
# 创建共享库
add_library(ext_util SHARED Util.cpp)

# PUBLIC：包含路径不仅对当前库可见，对使用该库的其他目标也可见
# 意味着链接到这个库之后不用加搜索目录了,因为已经加过了
target_include_directories(ext_util PUBLIC .)
```

```cmake
cmake_minimum_required(VERSION 3.5)
project(game)

# 这行代码告诉 CMake 使用 C++11 标准进行编译。
# CMAKE_CXX_STANDARD 是一个变量，CMake 将根据该变量设置编译器的选项，以确保所使用的 C++ 标准。
set(CMAKE_CXX_STANDARD 11)

# 这行代码表示 CMake 必须使用指定的 C++ 标准（在本例中为 C++11）。
# 如果将其设置为 True，CMake 会确保编译器支持 C++11。如果编译器不支持该标准，则会报错。
set(CMAKE_CXX_STANDARD_REQUIRED True)

# BOOL 类型功能开始或关闭 ON/OFF
option(PHYSICS_FEATURE "physics word" ON)


# 加载当前目录下的3rd目录的CMakeLists.txt文件
add_subdirectory(3rd)

# 创建可执行目标game
add_executable(game ../test.cpp)

target_compile_definitions(game PUBLIC "CC_PLATFORM_WIN32=1")
target_compile_definitions(game PUBLIC "CC_TARGET_PLATFORM=1")
if (PHYSICS_FEATURE)
    target_compile_definitions(game PUBLIC "PHYSICS_FEATURE")
endif()

# 在 CMake 中，可以使用 add_custom_command 或 add_custom_target 将 DLL 拷贝到可执行文件所在目录
# CMAKE_CURRENT_SOURCE_DIR 是当前正在处理的源目录的路径
add_custom_command(TARGET game POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E copy_if_different
    "${CMAKE_CURRENT_SOURCE_DIR}/3rd/libext_util.dll" $<TARGET_FILE_DIR:game>)

# 链接到可执行目标,注意顺序,先有目标才能链接
target_link_libraries(game PUBLIC ext_util)
```
================================================================ 
## 接口库
```cmake
# INTERFACE 指定这是一个接口库。接口库不会生成任何目标文件或可执行文件，但可以用于传播编译选项、编译特性或链接库。  
add_library(game_compiler_flags INTERFACE)
# target_compile_features 用于指定目标支持的编译特性
# cxx_std_11 表示要求使用 C++11 标准进行编译
target_compile_features(game_compiler_flags INTERFACE cxx_std_11)

#这段代码的作用是创建一个接口库 tutorial_compiler_flags，并要求所有链接到这个接口库的目标（如其他库或可执行文件）都使用 C++11 标准进行编译。这是一种管理和强制编译选项的方式，以确保一致性和可移植性。

#示例:
add_executable(my_app main.cpp)
target_link_libraries(game PUBLIC ext_util game_compiler_flags)
# 这样，my_app 在编译时将自动使用 C++11 标准
```
================================================================ 
## 生成器表达式
```cmake
#"$<COMPILE_LANG_AND_ID:CXX,ARMClang,AppleClang,Clang,GNU,LCC>" 是一个生成器表达式，它的作用是检查
# CXX 表示 C++ 语言。
# ARMClang、AppleClang、Clang、GNU、LCC 是一些可能的编译器名称。
# 这个表达式的结果是一个布尔值，如果当前的编译器是指定的某个编译器，并且使用 C++ 语言，那么 gcc_like_cxx 的值将为 TRUE，否则为 FALSE
set(gcc_like_cxx "$<COMPILE_LANG_AND_ID:CXX,ARMClang,AppleClang,Clang,GNU,LCC>")

# "$<COMPILE_LANG_AND_ID:CXX,MSVC>" 是另一个生成器表达式，用于检查编译器是否是 Microsoft Visual C++（MSVC）且编译语言为 C++。
set(msvc_cxx "$<COMPILE_LANG_AND_ID:CXX,MSVC>")



# "$<${gcc_like_cxx}:-Wall;-Wextra;-Wshadow;-Wformat=2;-Wunused>"
# $<${gcc_like_cxx}:...> 是一个条件生成器表达式，检查 gcc_like_cxx 的值。如果 gcc_like_cxx 为 TRUE，则添加后面的编译选项。
# -Wall：启用所有警告。
# -Wextra：启用额外的警告。
# -Wshadow：警告变量名被隐藏的情况。
# -Wformat=2：更严格的格式检查。
# -Wunused：警告未使用的变量和函数。
# b. "$<${msvc_cxx}:-W3>"
# $<${msvc_cxx}:...> 是另一个条件生成器表达式，检查 msvc_cxx 的值。如果 msvc_cxx 为 TRUE，则添加 -W3 作为编译选项。
# -W3 是 MSVC 编译器的一个选项，表示将警告级别设置为 3（相对较高的警告级别）。

# 如果使用的是 GCC 或类似的编译器（如 Clang），则启用一系列警告选项。
# 如果使用的是 MSVC 编译器，则设置较高的警告级别。
target_compile_options(tutorial_compiler_flags INTERFACE
  "$<${gcc_like_cxx}:-Wall;-Wextra;-Wshadow;-Wformat=2;-Wunused>"
  "$<${msvc_cxx}:-W3>"
) 
```